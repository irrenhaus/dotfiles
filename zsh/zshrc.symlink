#####################
## OH-MY-ZSH Config
#####################

# Path to your oh-my-zsh configuration.
ZSH=$HOME/.oh-my-zsh
ZSH_CUSTOM=$HOME/.oh-my-zsh-custom
DOTFILES=$HOME/.dotfiles

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
    source ~/.localrc
fi

# Load dotfiles ZSH configs
typeset -U config_files
config_files=("${(@f)$(find $DOTFILES -maxdepth 2 -name "*.zsh")}")

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
    echo $file | grep "symlink" 1>/dev/null 2>/dev/null || continue
    source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
    echo $file | grep "symlink" 1>/dev/null 2>/dev/null || continue
    source $file
done

#####################
## Activate oh-my-zsh
#####################
source $ZSH/oh-my-zsh.sh

# At this point compinit was run by oh-my-zsh

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
    echo $file | grep "symlink" 1>/dev/null 2>/dev/null || continue
    source $file
done

type rm | grep "alias" >/dev/null 2>/dev/null && unalias rm

unset config_files

source $HOME/.zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# Small workaround for virtualenvs not being found upon opening a new window
cd .

